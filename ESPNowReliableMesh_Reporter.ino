/*===========================================================================================================================
[Program Name]             :ESPNowReliableMesh_Reporter    
[Catagory]                 :mRTU, General device communication
[Description]              :ESPNow mesh with TTL and Package duplicate functionality with sensors for data colecting
[Author]                   :Ing. Bruno Villalobos R.
[Created using Arduino Ver :Arduino IDE 2.3.7
[Support and FeedBack]     :   
[Revison History]          :1.0 VersiOn Funcional, alfa 1   
[Date - Change]            :02/01/2026
===========================================================================================================================*/

#include <WiFi.h>
#include <esp_wifi.h>
#include <esp_now.h>
#include <vector>
#include "Adafruit_SHTC3.h"
#include "timerClass.h"
#define uS_TO_S_FACTOR 1000000  /* Conversion factor for micro seconds to seconds */
#define TIME_TO_SLEEP  1800        /* Time ESP32 will go to sleep (in seconds) */

// Yemp & Humedity sensor instance  
Adafruit_SHTC3 shtc3 = Adafruit_SHTC3();
timerClass timer1;
timerClass timer2;

// Expanded telemetry struct
typedef struct struct_message {
  int packetId;        // unique ID for duplicate detection
  int ttl;             // time-to-live (max hops)
  char sender[10];     // node identifier

  // Environmental data
  float temperature;   // °C
  float humidity;      // %

  // Vehicle-related data (non-CAN)
  float km_runned;     // distance traveled
  float petro_spent;   // fuel consumed

  // Suggested extras
  float speed;         // km/h
  float gps_lat;       // latitude
  float gps_lon;       // longitude
  float battery_volt;  // car battery voltage
  float accel_x;       // acceleration X
  float accel_y;       // acceleration Y
  float accel_z;       // acceleration Z
} struct_message;

sensors_event_t humidity, temp;
struct_message myData;
struct_message incomingData;

int packetCounter = 0;
std::vector<int> seenPackets;

// --- Duplicate detection helpers ---
bool isDuplicate(int packetId) {
  for (int pid : seenPackets) {
    if (pid == packetId) return true;
  }
  return false;
}

void rememberPacket(int packetId) {
  seenPackets.push_back(packetId);
  if (seenPackets.size() > 50) { // limit memory
    seenPackets.erase(seenPackets.begin());
  }
}

void addPeer(const uint8_t *mac) {
  esp_now_peer_info_t peerInfo = {};
  memcpy(peerInfo.peer_addr, mac, 6);
  
  peerInfo.channel = 6; // MUST match the forced channel
  peerInfo.encrypt = false;
  
  if (esp_now_add_peer(&peerInfo) == ESP_OK) {
    Serial.printf("Peer %02X:%02X:%02X:%02X:%02X:%02X added\n",
      mac[0], mac[1], mac[2], mac[3], mac[4], mac[5]);
  } else {
    Serial.println("Failed to add peer");
  }
}

// --- Receiver callback ---
void OnDataRecv(const esp_now_recv_info *recv_info, const uint8_t *incomingDataBytes, int len) {
  struct_message incomingData;
  // Use len guard to avoid over-read
  int copyLen = min(len, (int)sizeof(incomingData));
  memcpy(&incomingData, incomingDataBytes, copyLen);

  char macStr[18];
  snprintf(macStr, sizeof(macStr), "%02X:%02X:%02X:%02X:%02X:%02X",
           recv_info->src_addr[0], recv_info->src_addr[1], recv_info->src_addr[2],
           recv_info->src_addr[3], recv_info->src_addr[4], recv_info->src_addr[5]);

  Serial.println("--------------------------------------------------");
  Serial.printf("%s received from %s (MAC: %s)\n", myData.sender, incomingData.sender, macStr);
  Serial.printf("PacketID: %d | TTL: %d | Len: %d\n", incomingData.packetId, incomingData.ttl, len);
  Serial.printf("Temp: %.1f °C | Humidity: %.1f %%\n", incomingData.temperature, incomingData.humidity);
  Serial.printf("Distance: %.1f km | Fuel: %.1f L\n", incomingData.km_runned, incomingData.petro_spent);
  Serial.printf("Speed: %.1f km/h | GPS: (%.5f, %.5f)\n", incomingData.speed, incomingData.gps_lat, incomingData.gps_lon);
  Serial.printf("Battery: %.2f V\n", incomingData.battery_volt);
  Serial.printf("Accel: X=%.2f Y=%.2f Z=%.2f\n", incomingData.accel_x, incomingData.accel_y, incomingData.accel_z);
  Serial.println("--------------------------------------------------");

  // If the packed recived is the firts one then resent it 
  if (!isDuplicate(incomingData.packetId)) {
    rememberPacket(incomingData.packetId);

    if (strcmp(incomingData.sender, myData.sender) != 0 && incomingData.ttl > 0) {
      incomingData.ttl--;
      esp_err_t result = esp_now_send(NULL, (uint8_t *)&incomingData, sizeof(incomingData));
      if (result == ESP_OK) {
        Serial.println("Forwarded packet");
      } else {
        Serial.printf("Error forwarding packet, code: %d\n", result);
      }
    }
  } else {
    Serial.println("Duplicate packet ignored");
  }

}


// --- Setup ---
void setup() {
  Serial.begin(115200);
  WiFi.mode(WIFI_STA);
  // Force channel 6 (or any channel 1–13)
  esp_wifi_set_promiscuous(true);
  esp_wifi_set_channel(6, WIFI_SECOND_CHAN_NONE);
  esp_wifi_set_promiscuous(false);
  
  //ESP_NOW init
  esp_err_t espInitEstatus = esp_now_init();
  delay(100); // short pause after esp_now_init()
 
  if (espInitEstatus != ESP_OK) {
    Serial.println("Error initializing ESP-NOW");
    return;
  }

  // For troubleshooting 
  Serial.println (String(espInitEstatus));
  Serial.printf("Struct size: %d bytes\n", sizeof(myData));
  Serial.printf("Size of struct_message: %d\n", sizeof(struct_message));
  Serial.printf("Size of float: %d\n", sizeof(float));
  Serial.printf("Size of int: %d\n", sizeof(int));
  Serial.printf("Size of sender: %d\n", sizeof(myData.sender));
    
  //Adding peers
  //Node A: Waweshare 6DOR
  /*String Nodename = "NodeA";
  uint8_t macB[] = {0x94,0x54,0xC5,0x4D,0xA5,0xA0}; //ESP Wroom in breadboard
  uint8_t macC[] = {0x84,0xFC,0xE6,0x51,0x4E,0x3C}; // ESP-USB Dongle
  addPeer(macB);
  addPeer(macC);
  */
  
 
  //Node B: ESP32S3 USB-Dongle
  String  Nodename = "NodeB";
  uint8_t macA[] = {0xCC,0xBA,0x97,0x33,0xCD,0x64}; //Waweshare 6DOR
  uint8_t macC[] = {0x94,0x54,0xC5,0x4D,0xA5,0xA0}; //ESP Wroom in breadboard
  addPeer(macA);
  addPeer(macC);
  

  /*

  //Node C: ESP32-Wroom
  String Nodename = "NodeC";   
  uint8_t macA[] = {0xCC,0xBA,0x97,0x33,0xCD,0x64}; //Waweshare 6DOR
  uint8_t macB[] = {0x84,0xFC,0xE6,0x51,0x4E,0x3C}; // ESP-USB Dongle
  addPeer(macA);
  addPeer(macB);
  
*/
 
  Serial.println(WiFi.macAddress());
  delay(4000);

  // ESP_NOW call back function regitering
  esp_now_register_recv_cb(OnDataRecv);

  // Identify this node (change per board)
  strcpy(myData.sender, Nodename.c_str());   // NodeA, NodeB or NodeC

  // Sensors init ..............................................................
  //Temp & Humedity
  Wire.begin(16, 17);

  while (!Serial)
  delay(10);     // will pause Zero, Leonardo, etc until serial console opens
  
  Serial.println("SHTC3 test");
  if (! shtc3.begin()) {
    Serial.println("Couldn't find SHTC3");
    delay(4000);
  }

  Serial.println("Found SHTC3 sensor");
  // Wake up when GPIO 0 goes HIGH
  //int result2 = esp_sleep_enable_ext0_wakeup(GPIO_NUM_32, 1);  
  // Setup by timer
  int result2 = esp_sleep_enable_timer_wakeup(TIME_TO_SLEEP * uS_TO_S_FACTOR);  
  if (result2 == ESP_OK) {
  Serial.println("Input Wake-Up set successfully as wake-up source.");
  } 
  else {
  Serial.println("Failed to set Input Wake-Up as wake-up source.");
  }

}

// --- Broadcast telemetry ---
void broadcastData() {
  myData.packetId = packetCounter++;
  myData.ttl = 2;  // allow up to 2 hops

  // Dummy sensor values (replace with real sensors later)
  myData.temperature = temp.temperature;             // Sensor SHTC3
  myData.humidity    = humidity.relative_humidity;   // Sensor SHTC3
  myData.km_runned   = random(0, 1000) / 10.0;    // 0–100 km
  myData.petro_spent = random(0, 500) / 10.0;     // 0–50 L
  myData.speed       = random(0, 120);            // 0–120 km/h
  myData.gps_lat     = 51.8985;                   // Example latitude
  myData.gps_lon     = -8.4756;                   // Example longitude
  myData.battery_volt= random(115, 135) / 10.0;   // 11.5–13.5 V
  myData.accel_x     = random(-100, 100) / 10.0;
  myData.accel_y     = random(-100, 100) / 10.0;
  myData.accel_z     = random(-100, 100) / 10.0;

  // Broadcast packet
  esp_err_t result = esp_now_send(NULL, (uint8_t*)&myData, sizeof(myData));
  Serial.printf("Send result: %d\n", result);
  if (result == ESP_OK) {
    Serial.println("Broadcasted telemetry packet");
  } else {
    Serial.println("Error broadcasting packet");
  }
  
}

// --- Loop ---
void loop() {
  bool timer1Done;
  bool timer2Done;

  timer2Done = timer2.timer_LOOP(true, 5000);
  if (timer2Done){
    shtc3.getEvent(&humidity, &temp);// populate temp and humidity objects with fresh data
    //myData.temperature = temp.temperature;
    //myData.humidity = humidity.relative_humidity;
    Serial.print("Temperature: "); Serial.print(temp.temperature); Serial.println(" degrees C");
    Serial.print("Humidity: "); Serial.print(humidity.relative_humidity); Serial.println("% rH");       
    //Loops
    broadcastData();
  }

  timer1Done = timer1.timer_LOOP(true, 20000);
  if (timer1Done) {
  Serial.println("Getting Sleeping");
  esp_deep_sleep_start();     // Enter light sleep
  }
  
}
